Feisty Ebony Goat

high

# SolverVaults.depositToSymmio always revert on mainnet  if collateralToken is USDT

## Summary

Symmio's collateral token can only be USDC or USDT. USDT is a non-standard ERC20 on mainnet. Because `approve` of [[USDT](https://etherscan.io/token/0xdac17f958d2ee523a2206206994597c13d831ec7#code#L199)](https://etherscan.io/token/0xdac17f958d2ee523a2206206994597c13d831ec7#code#L199) does not return a value, then `IERC20(collateralTokenAddress).approve` will revert internally. Because IERC20 comes from openzeppelin library, its `approve` is defined as `function approve(address spender, uint256 value) external returns (bool)` where the return value is bool type with a size of 1 byte. The bytecode compiled by the solidity compiler from `IERC20(collateralTokenAddress).approve` will check whether the return size is 1 byte. If not, revert will occur.

## Vulnerability Detail

```solidity
File: solver-vaults\contracts\SolverVaults.sol
183:     function depositToSymmio(
184:         uint256 amount
185:     ) external onlyRole(DEPOSITOR_ROLE) whenNotPaused {
186:         uint256 contractBalance = IERC20(collateralTokenAddress).balanceOf(
187:             address(this)
188:         );
189:         require(
190:             contractBalance - lockedBalance >= amount,
191:             "SolverVault: Insufficient contract balance"
192:         );
193:         require(
194:->           IERC20(collateralTokenAddress).approve(address(symmio), amount),
195:             "SolverVault: Approve failed"
196:         );
197:         symmio.depositFor(solver, amount);
198:         emit DepositToSymmio(msg.sender, solver, amount);
199:     }
```

L194, the compiled code of `IERC20(collateralTokenAddress).approve(address(symmio), amount)` is similar to the following:

```solidity
(bool ret, bytes data) = collateralTokenAddress.call(abi.encodeWithSignature("approve(address,uint256)", symmio, amount);
if (ret) {
     if (data.length != 1) // since usdt.approve has no return value, so data.length = 0
     {
            revert;
     }
     return abi.decode(data, (bool));
} else {
     revert;
}
```

Copy the coded POC below to one project from Foundry and run `forge test -vvv` to prove this issue.

```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.20;

import "forge-std/Test.sol";
import "@openzeppelin/contracts/token/ERC20/extensions/IERC20Metadata.sol";
import "@openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol";

interface CheatCodes {
    function prank(address) external;
    function createSelectFork(string calldata,uint256) external returns(uint256);
}

contract ContractTest is DSTest{
    using SafeERC20 for IERC20;

    address USDT = 0xdAC17F958D2ee523a2206206994597C13D831ec7;
    CheatCodes cheats = CheatCodes(0x7109709ECfa91a80626fF3989D68f67F5b1DD12D);

    function setUp() public {
        cheats.createSelectFork("https://rpc.ankr.com/eth", 18925955);
    }

    function test() public {
        address user = address(0x12399543949349);
        cheats.prank(user);
        IERC20(USDT).approve(address(0x1111111111), 1000e6);
    }

    function testOkBySafeApprove() public {
        address user = address(0x12399543949349);
        cheats.prank(user);
        IERC20(USDT).forceApprove(address(0x1111111111), 1000e6);
    }
}
/**output
[FAIL. Reason: EvmError: Revert] test() (gas: 37087)
Traces:
  [37087] ContractTest::test()
    ├─ [0] VM::prank(0x0000000000000000000000000012399543949349)
    │   └─ ← ()
    ├─ [26953] 0xdAC17F958D2ee523a2206206994597C13D831ec7::approve(0x0000000000000000000000000000001111111111, 1000000000 [1e9])
    │   ├─ emit Approval(owner: 0x0000000000000000000000000012399543949349, spender: 0x0000000000000000000000000000001111111111, value: 1000000000 [1e9])
    │   └─ ← ()
    └─ ← EvmError: Revert

[PASS] testOkBySafeApprove() (gas: 37782)
Test result: FAILED. 1 passed; 1 failed; 0 skipped; finished in 297.17ms
**/
```

So, `SolverVaults.depositToSymmio` will never succeed.

## Impact

The main purpose of SolverVaults is to enable users to provide liquidity for a Hedger (solver) on the Symmio platform. Due to this issue, all funds deposited by users cannot be utilized, then Hedger cannot generate revenue for users. This does not cause a loss of funds, but **it breaks the core function of the contract**.

## Code Snippet

https://github.com/sherlock-audit/2023-12-symm-io/blob/main/solver-vaults/contracts/SolverVaults.sol#L194

## Tool used

Manual Review

## Recommendation

```solidity
File: solver-vaults\contracts\SolverVaults.sol
183:     function depositToSymmio(
184:         uint256 amount
185:     ) external onlyRole(DEPOSITOR_ROLE) whenNotPaused {
......
193:         require(
194:-            IERC20(collateralTokenAddress).approve(address(symmio), amount),
194:+            IERC20(collateralTokenAddress).forceApprove(address(symmio), amount),
195:             "SolverVault: Approve failed"
196:         );
.....
199:     }
```

&nbsp;