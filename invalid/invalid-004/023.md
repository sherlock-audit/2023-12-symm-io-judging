Ancient Mint Peacock

medium

# Loss of Yield: `SolverVaultShares` are not fungible.

## Summary

When a caller in possession of the [`BALANCER_ROLE`](https://github.com/sherlock-audit/2023-12-symm-io/blob/60ee22a4c598220821385cfb5eee43f40aafd5f1/solver-vaults/contracts/SolverVaults.sol#L24C31-L24C31) makes a call to [`acceptWithdrawRequest(uint256,uint256[],uint256)`](https://github.com/sherlock-audit/2023-12-symm-io/blob/60ee22a4c598220821385cfb5eee43f40aafd5f1/solver-vaults/contracts/SolverVaults.sol#L236), they may specify a [`paybackRatio`](https://github.com/sherlock-audit/2023-12-symm-io/blob/60ee22a4c598220821385cfb5eee43f40aafd5f1/solver-vaults/contracts/SolverVaults.sol#L239) to apply to a subset of specified withdrawal requests at their own discretion, meaning not all shares represent an equal claim to the vault collateral.

## Vulnerability Detail

Calls to [`acceptWithdrawRequest(uint256,uint256[],uint256)`](https://github.com/sherlock-audit/2023-12-symm-io/blob/60ee22a4c598220821385cfb5eee43f40aafd5f1/solver-vaults/contracts/SolverVaults.sol#L236) are permitted to specify a [`paybackRatio`](https://github.com/sherlock-audit/2023-12-symm-io/blob/60ee22a4c598220821385cfb5eee43f40aafd5f1/solver-vaults/contracts/SolverVaults.sol#L239) which is greater than or equal to the [`minimumPaybackRatio`](https://github.com/sherlock-audit/2023-12-symm-io/blob/60ee22a4c598220821385cfb5eee43f40aafd5f1/solver-vaults/contracts/SolverVaults.sol#L73C20-L73C39), allowing a subjective variable rate to be applied to specific subsets of withdrawal requests.

## Impact

1. Users are not fairly rewarded for their stake, and are instead compensated through mechanisms which are opaque to them and not enforced by the vault, resulting in an inconsistent distribution of yield.
2. For [`paybackRatio`](https://github.com/sherlock-audit/2023-12-symm-io/blob/60ee22a4c598220821385cfb5eee43f40aafd5f1/solver-vaults/contracts/SolverVaults.sol#L239)s over unity (`1e18`), withdrawal redemption can result in the rehypothecation and misappropriation of user deposits, which have not yet been deposited to SYMM IO, for rewards.

## Code Snippet

```solidity
function acceptWithdrawRequest(
    uint256 providedAmount,
    uint256[] memory _acceptedRequestIds,
    uint256 _paybackRatio
) external onlyRole(BALANCER_ROLE) whenNotPaused {
    IERC20(collateralTokenAddress).safeTransferFrom(
        msg.sender,
        address(this),
        providedAmount
    );
    require(
        _paybackRatio >= minimumPaybackRatio,
        "SolverVault: Payback ratio is too low"
    );
    uint256 totalRequiredBalance = lockedBalance;

    for (uint256 i = 0; i < _acceptedRequestIds.length; i++) {
        uint256 id = _acceptedRequestIds[i];
        require(
            id < withdrawRequests.length,
            "SolverVault: Invalid request ID"
        );
        require(
            withdrawRequests[id].status == RequestStatus.Pending,
            "SolverVault: Invalid accepted request"
        );
        totalRequiredBalance +=
            (withdrawRequests[id].amount * _paybackRatio) /
            1e18;
        withdrawRequests[id].status = RequestStatus.Ready;
        withdrawRequests[id].acceptedRatio = _paybackRatio;
    }

    require(
        IERC20(collateralTokenAddress).balanceOf(address(this)) >=
            totalRequiredBalance,
        "SolverVault: Insufficient contract balance"
    );
    lockedBalance = totalRequiredBalance;
    emit WithdrawRequestAcceptedEvent(
        providedAmount,
        _acceptedRequestIds,
        _paybackRatio
    );
}
```

## Tool used

Manual Review

## Recommendation

### [SolverVaults.sol](https://github.com/sherlock-audit/2023-12-symm-io/blob/main/solver-vaults/contracts/SolverVaults.sol)

The [`_paybackRatio`](https://github.com/sherlock-audit/2023-12-symm-io/blob/60ee22a4c598220821385cfb5eee43f40aafd5f1/solver-vaults/contracts/SolverVaults.sol#L239) should be fixed for all participants to ensure fairness and transparency of the operation for all users.

```diff
function acceptWithdrawRequest(
    uint256 providedAmount,
-    uint256[] memory _acceptedRequestIds,
+    uint256[] memory _acceptedRequestIds
-    uint256 _paybackRatio
) external onlyRole(BALANCER_ROLE) whenNotPaused {
    IERC20(collateralTokenAddress).safeTransferFrom(
        msg.sender,
        address(this),
        providedAmount
    );
-    require(
-        _paybackRatio >= minimumPaybackRatio,
-        "SolverVault: Payback ratio is too low"
-    );
    uint256 totalRequiredBalance = lockedBalance;

    for (uint256 i = 0; i < _acceptedRequestIds.length; i++) {
        uint256 id = _acceptedRequestIds[i];
        require(
            id < withdrawRequests.length,
            "SolverVault: Invalid request ID"
        );
        require(
            withdrawRequests[id].status == RequestStatus.Pending,
            "SolverVault: Invalid accepted request"
        );
        totalRequiredBalance +=
-            (withdrawRequests[id].amount * _paybackRatio) /
+            (withdrawRequests[id].amount * minimumPaybackRatio) /
            1e18;
        withdrawRequests[id].status = RequestStatus.Ready;
        withdrawRequests[id].acceptedRatio = _paybackRatio;
    }

    require(
        IERC20(collateralTokenAddress).balanceOf(address(this)) >=
            totalRequiredBalance,
        "SolverVault: Insufficient contract balance"
    );
    lockedBalance = totalRequiredBalance;
    emit WithdrawRequestAcceptedEvent(
        providedAmount,
        _acceptedRequestIds,
        _paybackRatio
    );
}
```