Ancient Mint Peacock

high

# Insolvency Risk: `SolverVaults.sol` can take on additional deposits even though they have outstanding obligations.

## Summary

When making a call to [`requestWithdraw(uint256,address)`](https://github.com/sherlock-audit/2023-12-symm-io/blob/60ee22a4c598220821385cfb5eee43f40aafd5f1/solver-vaults/contracts/SolverVaults.sol#L201), [`currentDeposit`](https://github.com/sherlock-audit/2023-12-symm-io/blob/60ee22a4c598220821385cfb5eee43f40aafd5f1/solver-vaults/contracts/SolverVaults.sol#L75) is immediately reduced by the total value of the [`amountInCollateralDecimals`](https://github.com/sherlock-audit/2023-12-symm-io/blob/60ee22a4c598220821385cfb5eee43f40aafd5f1/solver-vaults/contracts/SolverVaults.sol#L212). This enables the vault to continue accepting additional deposits even though the funds being requested to withdraw are _still_ deposited, resulting in an inadvertent breach of the [`depositLimit`](https://github.com/sherlock-audit/2023-12-symm-io/blob/60ee22a4c598220821385cfb5eee43f40aafd5f1/solver-vaults/contracts/SolverVaults.sol#L74).

## Vulnerability Detail

In the demonstration below, we show that a [`SolverVault`](https://github.com/sherlock-audit/2023-12-symm-io/blob/60ee22a4c598220821385cfb5eee43f40aafd5f1/solver-vaults/contracts/SolverVaults.sol) can take on double the maximum intended [`depositLimit`](https://github.com/sherlock-audit/2023-12-symm-io/blob/60ee22a4c598220821385cfb5eee43f40aafd5f1/solver-vaults/contracts/SolverVaults.sol#L74), due to the vault incorrectly reporting a safe deposit balance.

Further, this excess deposit balance can be misappropriated to meet existing financial obligations.

```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.13;

import {Test} from "forge-std/Test.sol";
import {console} from "forge-std/console.sol";

import {SolverVault} from "../contracts/SolverVaults.sol";
import {SolverVaultToken} from "../contracts/SolverVaultToken.sol";
import {MockERC20} from "../contracts/test/MockERC20.sol";
import {MockSymmio} from "../contracts/test/MockSymmio.sol";

contract SolverTest is Test {

    /* actors */
    address private constant _DEPLOYER = address(0x69);
    address private constant _SOLVER = address(0x420);
    address private constant _ALICE = address(0x1559);
    address private constant _BOB = address(0x721);

    function test_Insolvency() public {

        // Initialize a simple vault and distribute some test tokens.
        // Importantly, `maxDeposit` is `1e18` and `minPaybackRatio`
        // is also `1e18`.
        vm.startPrank(_DEPLOYER);

            MockERC20 mockERC20 = new MockERC20(18);
            MockSymmio mockSymmio = new MockSymmio(address(mockERC20));

            SolverVaultToken solverVaultToken = new SolverVaultToken();
            SolverVault solverVault = new SolverVault();

            solverVault.initialize(
                address(mockSymmio),
                address(solverVaultToken),
                _SOLVER,
                1e18,
                1e18
            );

            mockERC20.approve(address(solverVault), type(uint256).max);

            mockERC20.mint(_ALICE, 1e18);
            mockERC20.mint(_BOB, 1e18);

            solverVaultToken.grantRole(mockERC20.MINTER_ROLE(), address(solverVault));

        vm.stopPrank();

        vm.startPrank(_ALICE);

            mockERC20.approve(address(solverVault), type(uint256).max);
            solverVault.deposit(1e18);

        vm.stopPrank();

        // Here, Bob attempts to make a deposit, but due to Alice's interaction,
        // the `maxDeposit` has been reached causing the operation to correctly
        // fail.
        vm.startPrank(_BOB);

            mockERC20.approve(address(solverVault), type(uint256).max);

            vm.expectRevert("SolverVault: Deposit limit reached");
            solverVault.deposit(1e18);

        vm.stopPrank();

        // Let's have the deployer do something with Alice's funds.
        vm.prank(_DEPLOYER);
          solverVault.depositToSymmio(1e18);

        // Next, Alice requests a withdrawal:
        vm.startPrank(_ALICE);
            solverVaultToken.approve(address(solverVault), type(uint256).max);
            solverVault.requestWithdraw(solverVaultToken.balanceOf(_ALICE), _ALICE);
        vm.stopPrank();

        // At this stage, the vault has an obligation to pay Alice their
        // balance, as their funds are still deposited.
        // However, the vault continues to take on additional deposits in
        // excess of the `maxDeposits`:
        vm.prank(_BOB);
            solverVault.deposit(1e18) /* should_revert */;

        // Next, let's have the deployer redeem Alice's request.
        // Notice here, the deployer doesn't have to provide ANY
        // excess value to the vault, they can merely redeem Alice's
        // request using Bob's excess funds.
        vm.prank(_DEPLOYER);
            solverVault.acceptWithdrawRequest(0, new uint256[](1), 1e18);

    }
    
}
```

Inferring from this sequence, `_BOB` may then [`requestWithdraw(uint256,address)`](https://github.com/sherlock-audit/2023-12-symm-io/blob/60ee22a4c598220821385cfb5eee43f40aafd5f1/solver-vaults/contracts/SolverVaults.sol#L201), allowing the vault to take on further additional deposits and continue paying with additional user stake.

## Impact

This means that solvers which are currently unable to meet their financial obligations may take on excess deposits, and use these to pay back open requests to withdraw, resulting in:

1. The unintentional rehypothecation of user funds when paying back existing obligations.
2. The vault will incorrectly report as healthy even under extreme conditions, such as a bank run.
3. Vaults can accept more deposits than intended.
4. Deposits by users may not end up be meaningfully invested, leading to a loss of yield.

## Code Snippet

```solidity
function requestWithdraw(
    uint256 amount,
    address receiver
) external whenNotPaused {
    require(
        SolverVaultToken(solverVaultTokenAddress).balanceOf(msg.sender) >=
            amount,
        "SolverVault: Insufficient token balance"
    );
    SolverVaultToken(solverVaultTokenAddress).burnFrom(msg.sender, amount);

    uint256 amountInCollateralDecimals = collateralTokenDecimals >=
        solverVaultTokenDecimals
        ? amount *
            (10 ** (collateralTokenDecimals - solverVaultTokenDecimals))
        : amount /
            (10 ** (solverVaultTokenDecimals - collateralTokenDecimals));

    currentDeposit -= amountInCollateralDecimals;

    withdrawRequests.push(
        WithdrawRequest({
            receiver: receiver,
            amount: amountInCollateralDecimals,
            status: RequestStatus.Pending,
            acceptedRatio: 0
        })
    );
    emit WithdrawRequestEvent(
        withdrawRequests.length - 1,
        receiver,
        amountInCollateralDecimals
    );
}
```

## Tool used

Foundry

## Recommendation

To model a solution on the existing implementation, the [`currentDeposit`](https://github.com/sherlock-audit/2023-12-symm-io/blob/60ee22a4c598220821385cfb5eee43f40aafd5f1/solver-vaults/contracts/SolverVaults.sol#L75) should only be decreased for a user's contribution during a successful call to [`acceptWithdrawRequest(uint256,uint256[],uint256)`](https://github.com/sherlock-audit/2023-12-symm-io/blob/60ee22a4c598220821385cfb5eee43f40aafd5f1/solver-vaults/contracts/SolverVaults.sol#L236).

Alternatively, optimistic modifications to [`currentDeposit`](https://github.com/sherlock-audit/2023-12-symm-io/blob/60ee22a4c598220821385cfb5eee43f40aafd5f1/solver-vaults/contracts/SolverVaults.sol#L75) upon a withdrawal request may indeed still be made safely if the intention is to increase the liveliness of deposits.

If the difference between the initial deposit and the user's accepted [`minimumPaybackRatio`](https://github.com/sherlock-audit/2023-12-symm-io/blob/60ee22a4c598220821385cfb5eee43f40aafd5f1/solver-vaults/contracts/SolverVaults.sol#L73) is less than `1e18`, that is to say the user is willing to accept less than they originally deposited, this delta could be immediately subtracted from [`currentDeposit`](https://github.com/sherlock-audit/2023-12-symm-io/blob/60ee22a4c598220821385cfb5eee43f40aafd5f1/solver-vaults/contracts/SolverVaults.sol#L75) since the user has accepted the terms of withdrawal. This would require the remaining deposit balance to be subtracted in [`acceptWithdrawRequest(uint256,uint256[],uint256)`](https://github.com/sherlock-audit/2023-12-symm-io/blob/60ee22a4c598220821385cfb5eee43f40aafd5f1/solver-vaults/contracts/SolverVaults.sol#L236).