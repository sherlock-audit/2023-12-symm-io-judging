Ancient Mint Peacock

medium

# Griefing Vulnerability: Calls to `SolverVaults.sol#acceptWithdrawRequest` can be forced to fail by an attacker.

## Summary

Attempts to [`acceptWithdrawRequest(uint256,uint256[],uint256)`](https://github.com/sherlock-audit/2023-12-symm-io/blob/60ee22a4c598220821385cfb5eee43f40aafd5f1/solver-vaults/contracts/SolverVaults.sol#L236C17-L236C17) can be frontrun by an attacker to manipulate the [`balanceOf`](https://github.com/sherlock-audit/2023-12-symm-io/blob/60ee22a4c598220821385cfb5eee43f40aafd5f1/solver-vaults/contracts/SolverVaults.sol#L270) collateral in the vault, causing the transaction to revert with `"SolverVault: Insufficient contract balance"`.

## Vulnerability Detail

When a caller in possession of the [`BALANCER_ROLE`](https://github.com/sherlock-audit/2023-12-symm-io/blob/60ee22a4c598220821385cfb5eee43f40aafd5f1/solver-vaults/contracts/SolverVaults.sol#L24)  makes a call to [`acceptWithdrawRequest(uint256,uint256[],uint256)`](https://github.com/sherlock-audit/2023-12-symm-io/blob/60ee22a4c598220821385cfb5eee43f40aafd5f1/solver-vaults/contracts/SolverVaults.sol#L236C17-L236C17), they must specify a [`providedAmount`](https://github.com/sherlock-audit/2023-12-symm-io/blob/60ee22a4c598220821385cfb5eee43f40aafd5f1/solver-vaults/contracts/SolverVaults.sol#L237) of tokens to satisfy that the [`balanceOf`](https://github.com/sherlock-audit/2023-12-symm-io/blob/60ee22a4c598220821385cfb5eee43f40aafd5f1/solver-vaults/contracts/SolverVaults.sol#L270) the collateral tokens in the vault meet the [`totalRequiredBalance`](https://github.com/sherlock-audit/2023-12-symm-io/blob/60ee22a4c598220821385cfb5eee43f40aafd5f1/solver-vaults/contracts/SolverVaults.sol#L250), else the transaction is reverted.

However, this transaction can be front run.

An attacker can reduce the [`balanceOf`](https://github.com/sherlock-audit/2023-12-symm-io/blob/60ee22a4c598220821385cfb5eee43f40aafd5f1/solver-vaults/contracts/SolverVaults.sol#L270) through liquidating an accepted withdrawal position under their control via a call to [`claimForWithdrawRequest`](https://github.com/sherlock-audit/2023-12-symm-io/blob/60ee22a4c598220821385cfb5eee43f40aafd5f1/solver-vaults/contracts/SolverVaults.sol#L282C14-L282C37). The resulting balance of collateral tokens in the contract, combined with an insufficient [`providedAmount`](https://github.com/sherlock-audit/2023-12-symm-io/blob/60ee22a4c598220821385cfb5eee43f40aafd5f1/solver-vaults/contracts/SolverVaults.sol#L237), will force the transaction to revert.

## Impact

- Loss of gas for the balancer due to failed transaction costs, which can be exaggerated for large calldata (i.e. when accepting many open requests).
- Reduced liveliness for user withdrawals.
- Inferior economic efficiency if calls to `acceptWithdrawRequest(uint256,uint256[],uint256)` are inflated with supplemental extra value to combat execution-time variance.

## Code Snippet

```solidity
function acceptWithdrawRequest(
    uint256 providedAmount,
    uint256[] memory _acceptedRequestIds,
    uint256 _paybackRatio
) external onlyRole(BALANCER_ROLE) whenNotPaused {
    IERC20(collateralTokenAddress).safeTransferFrom(
        msg.sender,
        address(this),
        providedAmount
    );
    require(
        _paybackRatio >= minimumPaybackRatio,
        "SolverVault: Payback ratio is too low"
    );
    uint256 totalRequiredBalance = lockedBalance;

    for (uint256 i = 0; i < _acceptedRequestIds.length; i++) {
        uint256 id = _acceptedRequestIds[i];
        require(
            id < withdrawRequests.length,
            "SolverVault: Invalid request ID"
        );
        require(
            withdrawRequests[id].status == RequestStatus.Pending,
            "SolverVault: Invalid accepted request"
        );
        totalRequiredBalance +=
            (withdrawRequests[id].amount * _paybackRatio) /
            1e18;
        withdrawRequests[id].status = RequestStatus.Ready;
        withdrawRequests[id].acceptedRatio = _paybackRatio;
    }

    require(
        IERC20(collateralTokenAddress).balanceOf(address(this)) >=
            totalRequiredBalance,
        "SolverVault: Insufficient contract balance"
    );
    lockedBalance = totalRequiredBalance;
    emit WithdrawRequestAcceptedEvent(
        providedAmount,
        _acceptedRequestIds,
        _paybackRatio
    );
}
```

## Tool used

Manual Review

## Recommendation

Instead of manually specifying a [`providedAmount`](https://github.com/sherlock-audit/2023-12-symm-io/blob/60ee22a4c598220821385cfb5eee43f40aafd5f1/solver-vaults/contracts/SolverVaults.sol#L237) of surplus tokens to contribute to the vault off-chain, use a pull-payment strategy to calculate the additional amount of tokens required at execution time.

### [SolverVaults.sol](https://github.com/sherlock-audit/2023-12-symm-io/blob/main/solver-vaults/contracts/SolverVaults.sol)

```diff
function acceptWithdrawRequest(
-    uint256 providedAmount,
-    uint256[] memory _acceptedRequestIds,
+    uint256[] calldata _acceptedRequestIds,
    uint256 _paybackRatio
) external onlyRole(BALANCER_ROLE) whenNotPaused {
-    IERC20(collateralTokenAddress).safeTransferFrom(
-        msg.sender,
-        address(this),
-        providedAmount
-    );
    require(
        _paybackRatio >= minimumPaybackRatio,
        "SolverVault: Payback ratio is too low"
    );
    uint256 totalRequiredBalance = lockedBalance;

    for (uint256 i = 0; i < _acceptedRequestIds.length; i++) {
        uint256 id = _acceptedRequestIds[i];
        require(
            id < withdrawRequests.length,
            "SolverVault: Invalid request ID"
        );
        require(
            withdrawRequests[id].status == RequestStatus.Pending,
            "SolverVault: Invalid accepted request"
        );
        totalRequiredBalance +=
            (withdrawRequests[id].amount * _paybackRatio) /
            1e18;
        withdrawRequests[id].status = RequestStatus.Ready;
        withdrawRequests[id].acceptedRatio = _paybackRatio;
    }

-    require(
-        IERC20(collateralTokenAddress).balanceOf(address(this)) >=
-            totalRequiredBalance,
-        "SolverVault: Insufficient contract balance"
-    );
+   uint256 currentBalance = IERC20(collateralTokenAddress).balanceOf(address(this));
+
+   if (currentBalance < totalRequiredBalance)
+     IERC20(collateralTokenAddress).safeTransferFrom(
+          msg.sender,
+          address(this),
+          totalRequiredBalance - currentBalance
+     );
    lockedBalance = totalRequiredBalance;
    emit WithdrawRequestAcceptedEvent(
        providedAmount,
        _acceptedRequestIds,
        _paybackRatio
    );
}
```