Interesting Powder Kitten

medium

# Users not claiming withdrawals can hold the protocol insolvent by keeping high lockedBalance

## Summary

In the current implementation, users should manually initiate a `claimForWithdrawRequest` in order to claim their rewards.

## Vulnerability Detail

Users can `requestWithdraw` to make a request for withdrawing their deposited `collateralToken` by burning the taken `solverVaultTokenAddress`.

```solidity
function requestWithdraw(
        uint256 amount,
        address receiver
    ) external whenNotPaused {

        require(
            SolverVaultToken(solverVaultTokenAddress).balanceOf(msg.sender) >=
                amount,
            "SolverVault: Insufficient token balance"
        );

        SolverVaultToken(solverVaultTokenAddress).burnFrom(msg.sender, amount);

        uint256 amountInCollateralDecimals = collateralTokenDecimals >=
            solverVaultTokenDecimals
            ? amount *
                (10 ** (collateralTokenDecimals - solverVaultTokenDecimals))
            : amount /
                (10 ** (solverVaultTokenDecimals - collateralTokenDecimals));

        currentDeposit -= amountInCollateralDecimals;

        withdrawRequests.push(
            WithdrawRequest({
                receiver: receiver,
                amount: amountInCollateralDecimals,
                status: RequestStatus.Pending,
                acceptedRatio: 0
            })
        );
        emit WithdrawRequestEvent(
            withdrawRequests.length - 1,
            receiver,
            amountInCollateralDecimals
        );
    }

```
    
    Then, their request have to be accepted by a `balancer`: 
    
```solidity
    function acceptWithdrawRequest(
        uint256 providedAmount,
        uint256[] memory _acceptedRequestIds,
        uint256 _paybackRatio
    ) external onlyRole(BALANCER_ROLE) whenNotPaused {

        IERC20(collateralTokenAddress).safeTransferFrom(
            msg.sender,
            address(this),
            providedAmount
        );

        require(
            _paybackRatio >= minimumPaybackRatio,
            "SolverVault: Payback ratio is too low"
        );

        uint256 totalRequiredBalance = lockedBalance;

        for (uint256 i = 0; i < _acceptedRequestIds.length; i++) {

            uint256 id = _acceptedRequestIds[i];

            require(
                id < withdrawRequests.length,
                "SolverVault: Invalid request ID"
            );

            require(
                withdrawRequests[id].status == RequestStatus.Pending,
                "SolverVault: Invalid accepted request"
            );

            totalRequiredBalance +=
                (withdrawRequests[id].amount * _paybackRatio) /
                1e18;

            withdrawRequests[id].status = RequestStatus.Ready;
            withdrawRequests[id].acceptedRatio = _paybackRatio;
            
        }

        require(
            IERC20(collateralTokenAddress).balanceOf(address(this)) >=
                totalRequiredBalance,
            "SolverVault: Insufficient contract balance"
        );

        lockedBalance = totalRequiredBalance;

        emit WithdrawRequestAcceptedEvent(
            providedAmount,
            _acceptedRequestIds,
            _paybackRatio
        );
    }
 ```
    
    Let's walk through this function:
    
    1. `providedAmount` is used by balancer if there are not enough `collateralTokens` to cover the up-coming withdrawals.
    2. Keep in mind `uint256 totalRequiredBalance = lockedBalance;` because it's an important part of the vulnerability, `lockedBalance` is the currently accepted withdraws **BUT** not yet claimed by corresponding users.
    3. The `for` loop goes through the chosen ids by the balancer in the `_acceptedRequestIds`. Then the amount that is being requested to be withdrawn is added to `totalRequiredBalance`.
    
    `totalRequiredBalance +=
                (withdrawRequests[id].amount * _paybackRatio) /
                1e18;`
                
   4. Then there is a `require` statement that checks the `SolverVaults.sol` has enough `collateralToken` to cover the withdraws.
   
   -**The balance is checked against the `totalRequiredBalance`**
   
   5. `lockedBalance` is updated with the new `totalRequiredBalance(sum of accepted withdraws, even the ones not claimed)`.
   
  After that a user has to manually claim their withdraw by calling `claimForWithdrawRequest` to take back their `collateralToken` and **ONLY** then to lower the `lockedBalance`
  
  `uint256 amount = (request.amount * request.acceptedRatio) / 1e18;
        lockedBalance -= amount;`

## Impact

However this can be used by whales making big withdrawals or by a large group of people, to keep the `lockedBalance` high and keeping the contract insolvent - unable to make any more withdrawals to other users.

## Code Snippet

https://github.com/sherlock-audit/2023-12-symm-io/blob/main/solver-vaults/contracts/SolverVaults.sol#L201-L234

https://github.com/sherlock-audit/2023-12-symm-io/blob/main/solver-vaults/contracts/SolverVaults.sol#L236-L280

## Tool used

Manual Review

## Recommendation

Don't wait for users to manually `claimForWithdrawRequest` their `collateralToken` and do this step automatically in order to not give an edge to malicious groups holding the protocol insolvent
