Sour Laurel Poodle

medium

# SolverVaults.sol :: deposit() If a user sends small values of collateralToken and  collateralDecimals > solverVaultDecimals, the result may be the minting of 0 solverVaultTokens.

## Summary
The **`deposit()`** function enables users to send **collateralTokens** and mint **solverVaultTokens**. An issue arises when **`collateralDecimals > solverVaultDecimals`** and the user sends a small amount of **collateralTokens**, resulting in the minting of 0 **solverVaultTokens**.
## Vulnerability Detail
**`deposit()`** enables the send **collateralTokens** for **solverVaultTokens**.
```Solidity
function deposit(uint256 amount) external whenNotPaused {
        require(
            currentDeposit + amount <= depositLimit,
            "SolverVault: Deposit limit reached"
        );
        IERC20(collateralTokenAddress).safeTransferFrom(
            msg.sender,
            address(this),
            amount
        );
        uint256 amountInSolverVaultTokenDecimals = solverVaultTokenDecimals >=
            collateralTokenDecimals
            ? amount *
                (10 ** (solverVaultTokenDecimals - collateralTokenDecimals))
            : amount /
                (10 ** (collateralTokenDecimals - solverVaultTokenDecimals));
        SolverVaultToken(solverVaultTokenAddress).mint(
            msg.sender,
            amountInSolverVaultTokenDecimals
        );
        currentDeposit += amount;
        emit Deposit(msg.sender, amount);
    }
```
The decimals are dynamically adjusted based on **collateralTokenDecimals** and **solverVaultTokenDecimals**.
```Solidity
uint256 amountInSolverVaultTokenDecimals = solverVaultTokenDecimals >=
            collateralTokenDecimals
            ? amount *
                (10 ** (solverVaultTokenDecimals - collateralTokenDecimals))
            : amount /
                (10 ** (collateralTokenDecimals - solverVaultTokenDecimals));
```
An issue arises when **`collateralTokenDecimals > solverVaultTokenDecimals`**. In such cases, the second condition is triggered, leading to a division that may result in zero. Consequently, even if the user sends the correct amount of **collateralTokens**, the function may mint 0 **solverVaultTokens** due to the decimal adjustment.
```Solidity
SolverVaultToken(solverVaultTokenAddress).mint(
            msg.sender,
            amountInSolverVaultTokenDecimals
        );
```
To provide a clearer explanation of the issue, let's use an example:

Suppose we have the following parameters:

- collateralTokenDecimals = 10
- solverVaultTokenDecimals = 5
- amount = 1e4

The calculation for amountInSolverVaultTokenDecimals is expressed as:

**`amountInSolverVaultTokenDecimals = amount / (10 ** (collateralTokenDecimals - solverVaultTokenDecimals));`**
**`amountInSolverVaultTokenDecimals = 1e4 / 10 ** (10 - 5) = 1e4 / 1e5 = 0`**

In this case 0 **solverVaultTokens** will be minted to the user.
## POC
To execute the POC, copy the provided code into **`SolverVault.test.ts`** and make the following modification on line 20. 
This adjustment is made to set **collateralDecimals** greater than **solverVaultTokenDecimals**.
```diff
- let collateralDecimals: bigint = 6n, solverVaultTokenDecimals: bigint = 8n
+ let collateralDecimals: bigint = 10n, solverVaultTokenDecimals: bigint = 5n
```

```Solidity
describe("deposit", function () {
        //small amount of collateralTokens
        const depositAmount = decimal(1, collateralDecimals - 6n)

        beforeEach(async function () {
            await mintFor(user, depositAmount)
        })

        it("receive 0 vaultTokens for small amount of collateral", async function () {
            //deposit the amount
            await expect(solverVault.connect(user).deposit(depositAmount))
                .to.emit(solverVault, "Deposit")
                .withArgs(await user.getAddress(), depositAmount)
            
            //user receives 0 solverVaultTokens 
            expect(await solverVaultToken.balanceOf(await user.getAddress())).to.equal(0)
            //protocol receives the collateralTokens
            expect(await collateralToken.balanceOf(await solverVault.getAddress())).to.equal(depositAmount)
            expect(await solverVault.currentDeposit()).to.equal(depositAmount)
        })
    })
```
## Impact
Users may experience a loss of funds as they will not have **solverVaultTokens** available to claim their **collateralTokens**.
## Code Snippet
https://github.com/sherlock-audit/2023-12-symm-io/blob/60ee22a4c598220821385cfb5eee43f40aafd5f1/solver-vaults/contracts/SolverVaults.sol#L158-L181
## Tool used
Manual Review
## Recommendation
Include a require to verify that the amount is not equal to zero.
```Solidity
require(amount != 0, "Amount to mint 0");
```