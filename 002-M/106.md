Restless Brown Bobcat

high

# Using ERC20 approve() will revert USDT transfers which may lead to USDT funds getting locked.

## Summary
- As stated here in [Q&A](https://github.com/sherlock-audit/2023-12-symm-io-0xgreywolf?tab=readme-ov-file#q-which-erc20-tokens-do-you-expect-will-interact-with-the-smart-contracts) USDT and USDC will be used to interact with the scoped contracts. 
- In this [code block](https://github.com/sherlock-audit/2023-12-symm-io/blob/main/solver-vaults/contracts/SolverVaults.sol#L193C8-L196C11), ERC20 `approve()` is used, thus reverting USDT transfer of funds from `SolverVaults` contract to `Symmio` contract via function `depositToSymmio()`. Since it is being reverted, USDT funds could be locked within the `SolverVaults` contract.
- Surprisingly, the safeERC20 library is already imported but didn't use the supposed proper function which is the `forceApprove()`.

## Vulnerability Detail
- This revert happens because USDT does not follow the ERC20 standard where `approve()` should return a boolean value. Unfortunately, USDT does not return anything on `approve()` call which led to the revert.
- Specifically in `SolverVaults` contract, this revert happens on `depositToSymmio()` call when the collateral is USDT.
- For quick reference, here's the code block below and here's the link to the repo: [here](https://github.com/sherlock-audit/2023-12-symm-io/blob/main/solver-vaults/contracts/SolverVaults.sol#L193C8-L196C11) 

```solidity
require(
    IERC20(collateralTokenAddress).approve(address(symmio), amount),
    "SolverVault: Approve failed"
);
```
- To demonstrate this revert, here's a Foundry test (PoC) on a Mainnet fork.
### Proof-Of-Code / Concept (Foundry)
- INTRO 
    - The purpose of this PoC is to show that USDT will revert when `approve()` is called.
    - To show this, the block of code "in subject" is isolated and placed in the functions in order to simulate:
        - that function reverts when USDT is involved (as a collateral)
        - in contrast, function does not revert when USDC is involved (as a collateral)
- SETUP
    - Integrate Foundry on this Hardhat project: https://hardhat.org/hardhat-runner/docs/advanced/hardhat-and-foundry
    - Create a Solidity file in the test folder.
    - Copy and paste the code below.
    - On the terminal run the following: 
        `$ FORK_URL=https://eth-mainnet.g.alchemy.com/v2/[YOUR_KEY]`
        `$ forge test --fork-url $FORK_URL --match-path test/TestCodeBlockMainnetFork.sol -vvvv`
        
- CODE
    ```solidity
    // SPDX-License-Identifier: MIT
    // This contract will run on ETH MAINNET FORK.

    pragma solidity 0.8.20;

    import "forge-std/Test.sol";
    import "forge-std/console.sol";
    import "@openzeppelin/contracts/token/ERC20/IERC20.sol";

    contract TestCodeBlockMainnetFork is Test {
        IERC20 public usdt;
        IERC20 public usdc;

        // mainnet addresses of USDT and USDC
        address public constant _USDT_ = 0xdAC17F958D2ee523a2206206994597C13D831ec7;
        address public constant _USDC_ = 0xA0b86991c6218b36c1d19D4a2e9Eb0cE3606eB48;
        uint256 public constant AMOUNT = 2_000_000_000;
        address public dummySymmio;

        function setUp() public {
            usdt = IERC20(_USDT_);
            usdc = IERC20(_USDC_);

            // sends usdc and usdt to this contract
            deal(address(usdt), address(this), AMOUNT);
            deal(address(usdc), address(this), AMOUNT);

            // dummy address of Symmio
            dummySymmio = address(13);  
        }

        // This test will show that USDT `approve` will fail.
        // This function testFail* is expecting a fail.
        function testFail_usingApprove_USDT() external {        
            // checks if usdt has been sent to this contract
            console.log("usdt_balance_of_this_contract:", IERC20(address(usdt)).balanceOf(address(this)));

            // this is the code block from SolverVaults.sol        
            require(
                IERC20(address(usdt)).approve(dummySymmio, AMOUNT),
                "SolverVault: Approve failed"
            );
        }

        // This test will show that USDC `approve` will pass.
        // The test* is expecting a pass.
        function test_usingApprove_USDC() external {          
            // checks if usdc has been sent to this contract
            console.log("usdc_balance_of_this_contract:", IERC20(address(usdc)).balanceOf(address(this)));

            // this is the code block from SolverVaults.sol
            require(
                IERC20(address(usdc)).approve(dummySymmio, AMOUNT),
                "SolverVault: Approve failed"
            );
        }
    }
    ```
- RESULT
    - USDC returns true on approval
![image](https://github.com/sherlock-audit/2023-12-symm-io-0xgreywolf/assets/143251524/0edc666f-3b1b-41e0-ae6f-a29e03c8bfe1)

    - While USDT does not return anything and reverts
      ![image](https://github.com/sherlock-audit/2023-12-symm-io-0xgreywolf/assets/143251524/b7971a2c-827e-44e2-acc5-211aaa253ce7)
## Impact
USDT funds may be locked within the `SolverVaults.sol` contract. 

## Code Snippet
[SolverVaults.sol :: depositToSymmio()](https://github.com/sherlock-audit/2023-12-symm-io/blob/main/solver-vaults/contracts/SolverVaults.sol#L193C8-L196C11)

## Tool used
- Manual Review

## Recommendation
- Use the safeERC20's `forceApprove()`.